apply plugin: 'com.android.application'

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile project(':core')
    compile 'com.android.support:multidex:1.0.0'
    testCompile 'org.robolectric:robolectric:3.+'
    testCompile 'junit:junit:4.+'
    //compile 'com.github.deano2390:MaterialShowcaseView:1.0.5'
    compile project(':concurtourguide')
}


android {

    defaultConfig {
        minSdkVersion lowestSdkSupportVersion
        targetSdkVersion latestSdkVersion
        versionCode System.getenv("VERSION_CODE") as Integer ?: 2999
        versionName "9.26.0"
        multiDexEnabled true
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"

        applicationId "com.concur.breeze"
        resValue "string", "app_name", "Concur"

        buildConfigField "String", "AUTHORITY", '"com.concur.mobile"'
        buildConfigField "String", "ACC_TYPE", '"com.concur.mobile"'

        resValue "string", "authority_expense", "com.concur.mobile.platform.expense"
        resValue "string", "authority_config", "com.concur.mobile.platform.config"
        resValue "string", "authority_travel", "com.concur.mobile.platform.travel"
        resValue "string", "authenticator_acc_type", "com.concur.mobile.platform.common.account"

    }

    dexOptions {
        preDexLibraries true // increases build speed (2-4s)
        incremental true // increases build speed
        javaMaxHeapSize "4g"
        //jumboMode true
    }

    lintOptions {
        abortOnError false
    }


    splits {
        abi {
            enable true
            reset()
            include 'armeabi', 'x86'
            universalApk true
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'plugin.properties'
        exclude 'lib/armeabi/libdatabase_sqlcipher.so'
        exclude 'lib/armeabi/libsqlcipher_android.so'
        exclude 'lib/armeabi/libstlport_shared.so'
        exclude 'lib/x86/libdatabase_sqlcipher.so'
        exclude 'lib/x86/libsqlcipher_android.so'
        exclude 'lib/x86/libstlport_shared.so'
        exclude 'lib/sources'
        exclude 'test/AndroidManifest.xml'
    }

    //For multi build purpose uncomment following block
    //Please comment out following block before merge back to 'dev' branch.
    /*##############   #####################     ##########################     ###################*/

    productFlavors {
        production {

        }
        developerReleaseTest {
            applicationId "com.concur.breeze.developer.release.test"
            resValue "string", "app_name", "devRelease"

            buildConfigField "String", "AUTHORITY", "\"${applicationId}\""
            buildConfigField "String", "ACC_TYPE", "\"${applicationId}\""

            resValue "string", "authority_expense", applicationId + ".platform.expense"
            resValue "string", "authority_config", applicationId + ".platform.config"
            resValue "string", "authority_travel", applicationId + ".platform.travel"
            resValue "string", "authenticator_acc_type", applicationId + ".platform.common.account"

        }
    }
    /*##############   #####################     ##########################     ###################*/

}

// map for the version code
//Ensures architecture specific APKs have a higher version code
//(otherwise an x86 build would end up using the arm build, which x86 devices can runext.
import com.android.build.OutputFile

def versionCodes = [armeabi: 1, x86: 3]
int versionIndex = 1; // This is used so each version code is unique.

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        if ("Release" == System.getenv("art_package_type")) {
            println(">> Skipping versionCodeOverride: " + output);
        } else {
            int abiVersionCode = versionCodes.get(output.getFilter(OutputFile.ABI)) ?: versionIndex
            output.versionCodeOverride = (abiVersionCode * 1000000) + (android.defaultConfig.versionCode ?: versionIndex)
            versionIndex++
        }
    }

}
